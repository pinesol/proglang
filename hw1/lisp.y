/* lisp.y
Lisp Parser
Alex Pine
Programming Languages, Summer 2015
*/
%{
#include <iostream>

int yylex();
int yyerror(const char* p) { std::cerr << "error: " << p << std::endl; return 0; };
%}

%union {
    int val;
};

%start prog

%token LPAREN RPAREN
%token PLUS MINUS MUL DIV
%token <val> NUM

%type <val> list expr plus_func mul_func arg

/* Order of directives will determine the precedence. */
%left PLUS MINUS    /* left means left-associativity. */
%left DIV MUL

%%

prog : list                             { std::cout << $1 << std::endl; }
     ;

list : LPAREN expr RPAREN               { $$ = $2; }

expr : PLUS plus_func                   { $$ = $2; }
     | MUL mul_func                     { $$ = $2; }
     | MINUS arg arg                    { $$ = $2 - $3; }
     | DIV arg arg                      { $$ = $2 / $3; }
     ;

arg : NUM
    | list
    ;

plus_func : plus_func arg               { $$ = $1 + $2; }
          | arg
          ;

mul_func : mul_func arg                 { $$ = $1 * $2; }
         | arg
         ;

%%

int main()
{
    yyparse(); // A parsing function that will be generated by Bison.
    return 0;
}
